def scalaMajorVersion = gradle.ext.scalaMajorVersion
def scalaMinorVersion = gradle.ext.scalaMinorVersion

buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url "https://dl.bintray.com/ngbinh/maven"
        }
    }
    dependencies {
        classpath "org.github.ngbinh.scalastyle:gradle-scalastyle-plugin_${gradle.ext.scalaMajorVersion}:1.0.1"
        classpath "gradle.plugin.com.hierynomus.gradle.plugins:license-gradle-plugin:0.15.0"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "scala"
    apply plugin: "maven"
    apply plugin: "maven-publish"
    apply plugin: "scalaStyle"
    apply plugin: "com.github.hierynomus.license"
    apply plugin: "signing"

    group = "com.sumologic.shellbase"
    description = "Sumo Logic's Scala-based interactive shell framework"

    version = '1.5.4'

    sourceCompatibility = javaSourceVersion
    targetCompatibility = javaTargetVersion

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        testCompile "org.scalatest:scalatest_${scalaMajorVersion}:${scalatestVersion}"
        testCompile "junit:junit:${junitVersion}"
        testCompile "org.mockito:mockito-core:${mockitoVersion}"
    }

    compileScala {
        scalaCompileOptions.additionalParameters = [
                "-feature",
                "-target:jvm-1.8",
                "-deprecation",
                "-language:implicitConversions",
                "-unchecked",
                // TODO: (fdaca 2019-11-18) please consider repairing warnings and enabling this
                //"-Xfatal-warnings"
        ]
    }

    test {
        maxHeapSize = '2g'
        testLogging {
            events 'started', 'failed', 'passed', 'skipped'
            showStandardStreams = true
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

subprojects {
    ext.gitCommitIdAbbrev = System.getenv('GIT_COMMIT_ID_ABBREV') ?: 'git rev-parse --verify --short=12 HEAD'.execute().text.trim()

    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier.set('sources')
        from(sourceSets.main.allJava) {
            expand(project: [version: project.version], git: [commit: [id: [abbrev: gitCommitIdAbbrev]]])
        }
        from(sourceSets.main.allScala)
    }

    task javadocJar(type: Jar, dependsOn: scaladoc) {
        from javadoc
        from scaladoc
        archiveClassifier.set('javadoc')
    }

    license {
        header rootProject.file('src/etc/header.txt')
        strictCheck true
        exclude "**/warning.txt"
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId(project.group)
                artifactId(project.name + "_${scalaMajorVersion}")
                version(project.version)

                pom {
                    name = project.name
                    description = project.description
                    url = 'https://github.com/SumoLogic/shellbase'

                    licenses {
                        license {
                            name = 'Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'goledzki'
                            name = 'Greg Oledzki'
                            organization = 'Sumo Logic'
                        }
                    }
                    scm {
                        connection = 'scm:git:git@github.com:SumoLogic/shellbase.git'
                        developerConnection = 'scm:git:git@github.com:SumoLogic/shellbase.git'
                        url = 'https://github.com/SumoLogic/shellbase'
                        tag = 'HEAD'
                    }
                }

                from(components.java)
                artifact(sourcesJar)
                artifact(javadocJar)
            }
        }
        repositories {
            maven {
                url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                credentials {
                    username project.findProperty('nexus_username') ?: ""
                    password project.findProperty('nexus_password') ?: ""
                }
            }
        }
    }

    signing {
        useGpgCmd()
        sign publishing.publications.maven
    }
}

project(':shellbase-core') {
    dependencies {
        compile "joda-time:joda-time:${jodaTimeVersion}"
        compile "org.slf4j:slf4j-log4j12:${slf4jVersion}"
        compile "commons-cli:commons-cli:${commonsCliVersion}"
        compile "commons-io:commons-io:${commonsIoVersion}"
        compile "jline:jline:${jlineVersion}"
        compile "org.apache.velocity:velocity:${velocityVersion}"
        compile "org.scala-lang:scala-library:${scalaMajorVersion}.${scalaMinorVersion}"
    }
}

project(':shellbase-example') {
    dependencies {
        compile project(':shellbase-core')
    }
}

project(':shellbase-slack') {
    dependencies {
        compile project(':shellbase-core')
        compile "com.flyberrycapital:scala-slack_${scalaMajorVersion}:${scalaSlackVersion}"
    }
}
